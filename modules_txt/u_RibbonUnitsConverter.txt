' Units converter from Leonid Igrevsky
Option Base 0
'btn3 (компонент: button, атрибут: onAction), 2007
Sub btn_units_converter(control As IRibbonControl)
    Dim num As Integer
    Call RunThis
End Sub
Private Sub MakeParameter( _
        Dst As range, _
        ParameterName, _
        Dimensions() As Variant, _
        DefaultValue, _
        ExcelCellName)
        
' Cоздает поле ввода параметра с возможностью выбора единиц измерения и пересчетом в СИ и в другие единицы измерения
' Dst - ячейка, куда будет помещен вывод, займет 8 ячеек вправо
' ParameterName - название параметра, например "Проницаемость"\
' Dimensions() - динамический массив строк с разделителем вертикальной чертой, например "мД|x*1.02e-15|x/1.02e-15|L"
' массив должен начинаться с нулевого элемента (option base 0), х - латинская буква для формулы перевода
' первая размерность в массиве должна соответствовать размерности в системе СИ
' размерность | формула для перевода в СИ | формула для перевода из СИ | L или R
' если в конце указана буква L - это размерность по умолчанию слева, R - справа
' если две буквы LR - размерности слева и справа по умолчанию будут одинаковые
' если хотите что-то сломать, задав другой формат, то будьте уверены - сломаете! удачи!
' DefaultValue - значение параметра по умолчанию
' ExcelCellName - имя ячейки со значением в системе СИ, можно использовать в формулах
    Dst.Value = ParameterName
    Dim s() As String ' Для подстрок
    sOutD = "" ' Строка размерностей
    sOutL = "=IFS(RC[-2]="""",""""" ' Строка формулы перевода
    sOutR = "=IFS(RC[-2]="""","""""
    For Each M In Dimensions
        s = Split(M, "|") ' Будут созданы строки s(0) s(1) s(2) и возможно s(3)
        s(1) = Replace(s(1), "x", "RC[-2]") ' Букву х нужно заменить на относительную ссылку
        s(2) = Replace(s(2), "x", "RC[-2]")
        sOutD = sOutD & "," & s(0) ' Названия размерностей
        sOutL = sOutL & "," & "RC[-1]=""" & s(0) & """," & s(1) ' Формулы пересчета для внесения в формулу IFS
        sOutR = sOutR & "," & "RC[+1]=""" & s(0) & """," & s(2)
        If UBound(s) > 2 Then
            If s(3) = "L" Then DefaultDimensionL = s(0) ' Размерность по умолчанию слева
            If s(3) = "R" Then DefaultDimensionR = s(0) ' Размерность по умолчанию справа
            If s(3) = "LR" Then
                DefaultDimensionL = s(0) ' Размерность по умолчанию и там и там
                DefaultDimensionR = s(0)
            End If
        End If
    Next
    Dst.Cells(1, 3).Value = DefaultValue ' Значение по умолчанию
    Dst.Cells(1, 4).Validation.Delete ' Выпадающий список размерностей
    Dst.Cells(1, 4).Validation.Add Type:=xlValidateList, Formula1:=sOutD
    Dst.Cells(1, 4).Value = DefaultDimensionL ' Размерность по умолчанию
    Dst.Cells(1, 5).Formula = sOutL & ")" ' Формула для пересчета в СИ
    Dst.Cells(1, 5).Font.ThemeColor = xlThemeColorDark1
    Dst.Cells(1, 5).Font.TintAndShade = -0.5
    s = Split(Dimensions(0), "|")
    Dst.Cells(1, 6).Value = s(0) ' Размерность для СИ
    Dst.Cells(1, 6).Font.ThemeColor = xlThemeColorDark1
    Dst.Cells(1, 6).Font.TintAndShade = -0.5
    'Names.Add name:=ExcelCellName, RefersTo:=Dst.Cells(1, 5) ' Уникальное имя параметра в системе СИ
    Dst.Cells(1, 7).Formula = sOutR & ")" ' Формула для пересчета из СИ
    Dst.Cells(1, 7).Font.ThemeColor = xlThemeColorDark1
    Dst.Cells(1, 7).Font.TintAndShade = -0.5
    Dst.Cells(1, 8).Validation.Delete ' Выпадающий список размерностей
    Dst.Cells(1, 8).Validation.Add Type:=xlValidateList, Formula1:=sOutD
    Dst.Cells(1, 8).Value = DefaultDimensionR ' Размерность по умолчанию
    
End Sub
Public Sub RunThis()
    
' Действие описано в функции MakeParameter
    
    Dim d() As Variant
    
    Set Place = ActiveCell
    'Set Place = [a1] ' В отладочных целях
    
    d = Array( _
        "Па" & ChrW(183) & "с" & "|x|x", _
        "мПа" & ChrW(183) & "с" & "|x/1000|x*1000|R", _
        "сП|x/1000|x*1000|L")
    MakeParameter Place.offset(0, 0), "Вязкость", d, 5, "mu"
    
    d = Array( _
        "Па|x|x", _
        "МПа|x*1000000|x/1000000", _
        "ат|x*98066.5|x/98066.5", _
        "кгс/см" & ChrW(178) & "|x*98066.5|x/98066.5", _
        "атм|x*101325|x/101325", _
        "psi|x*6894.76|x/6894.76|R", _
        "бар|x*100000|x/100000|L")
    MakeParameter Place.offset(1, 0), "Давление", d, 200, "Pres"
    d = Array( _
        "м" & ChrW(178) & "|x|x", _
        "Д|x*1.02e-12|x/1.02e-12", _
        "мД|x*1.02e-15|x/1.02e-15|LR")
    MakeParameter Place.offset(2, 0), "Проницаемость", d, 100, "Kres"
    d = Array( _
        ChrW(176) & "К|x|x", _
        ChrW(176) & "С|x+273.15|x-273.15|L", _
        ChrW(176) & "F|(x-32)*5/9+273.15|(x-273.15)*9/5+32|R")
    MakeParameter Place.offset(3, 0), "Температура", d, 100, "Tres"
    d = Array( _
        "м" & ChrW(179) & "/м" & ChrW(179) & "|x|x|L", _
        "scft/bbl|x*0.02832/0.158987|x/0.02832*0.158987", _
        "ft" & ChrW(179) & "/bbl|x*0.02832/0.158987|x/0.02832*0.158987|R")
    MakeParameter Place.offset(4, 0), "Газосодержание", d, 50, "GOR"
    d = Array( _
        "м|x|x", _
        "мм|x/1000|x*1000|L", _
        "см|x/100|x*100", _
        "in|x*0.0254|x/0.0254|R", _
        "ft|x*0.3048|x/0.3048")
    MakeParameter Place.offset(5, 0), "Расстояние", d, 73, "Rw"
    d = Array( _
        "м" & ChrW(179) & "/с|x|x", _
        "м" & ChrW(179) & "/сут|x/86400|x*86400|L", _
        "л/мин|x/60000|x*60000", _
        "л/сек|x/1000|x*1000", _
        "bpd|x*0.158987/86400|x/0.158987*86400|R", _
        "ft" & ChrW(179) & "/сут|x*0.02832/86400|x/0.02832*86400")
    MakeParameter Place.offset(6, 0), "Дебит", d, 100, "Q"
    d = Array( _
        "кг/м" & ChrW(179) & "|x|x|L", _
        "г/cм" & ChrW(179) & "|x*1000|x/1000", _
        "lb/gal|x*119.8|x/119.8", _
        "lb/in" & ChrW(179) & "|x*27679.9|x/27679.9", _
        "lb/ft" & ChrW(179) & "|x*16.01846|x/16.01846", _
        ChrW(176) & "API|141.5*1000/(131.5+x)|141.5*1000/x-131.5|R")
    MakeParameter Place.offset(7, 0), "Плотность", d, 850, "ROo"
End Sub
Function UniflocConvert( _
        x As Double, _
        ConvertFrom As range, _
        ConvertTo As range) As Double
' функция не должна вызываться самостоятельно, она пока и не написана
' ее правильно должна заполнять функция MakeParameter
' с помощью нее можно будет отказаться от столбика в системе СИ
' x - число для конвертации (колонка С)
' ConvertFrom - единицы измерения слева (колонка D)
' ConvertTo - единицы измерения справа (колонка F)
' колонку Е нужно будет заблокировать
' формулы перевода можно передавать прямо в функцию как две строки
End Function

