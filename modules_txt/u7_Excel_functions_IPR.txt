'=======================================================================================
'Unifloc 7.27b primavera                                     khabibullinra@gmail.com
'Petroleum engineering calculations modules (macroses)
'2000 - 2021
'
'=======================================================================================
' базовые функции дл€ проведени€ расчетов из интерфейса Excel
Option Explicit
' ==============  функции дл€ расчета пласта ==========================
' =====================================================================
'description_to_manual      - дл€ автогенерации описани€ - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет дебита по давлению и продуктивности
Public Function IPR_q_liq_sm3day( _
                 ByVal pi_sm3dayatm As Double, _
                 ByVal p_res_atma As Double, _
                 ByVal p_wf_atma As Double, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1)
' pi_sm3dayatm   - коэффициент продуктивности, ст.м3/сут/атм
' p_res_atma      - пластовое давление, абс. атм
' p_wf_atma       - забойное давление, абс. атм
' fw_perc        - обводненность, %
' pb_atma        - давление насыщени€, абс. атм
' результат      - значение дебита жидкости, ст.м3/сут
'description_end
On Error GoTo err1:
    Dim res As New CReservoirVogel
    If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид додсчитает по коррел€ции значение
    res.InitProp p_res_atma, pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    
    IPR_q_liq_sm3day = res.calc_q_liq_sm3day(p_wf_atma)
    Set res = Nothing
    
    
    Exit Function
err1:
    IPR_q_liq_sm3day = -1
    add_log_msg "Error:IPR_q_liq_sm3day:" & Err.Description
End Function
'description_to_manual      - дл€ автогенерации описани€ - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет забойного давлени€ по дебиту и продуктивности
Public Function IPR_p_wf_atma( _
                 ByVal pi_sm3dayatm As Double, _
                 ByVal p_res_atma As Double, _
                 ByVal q_liq_sm3day As Double, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1)
' pi_sm3dayatm   - коэффициент продуктивности, ст.м3/сут/атм
' p_res_atma      - пластовое давление, абс. атм
' q_liq_sm3day    - дебит жидкости скважины на поверхности, ст.м3/сут
' fw_perc        - обводненность, %
' pb_atma        - давление насыщени€, абс. атм
' результат      - значение забойного давлени€, абс. атм
'description_end
On Error GoTo err1:
    Dim res As New CReservoirVogel
    If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид подсчитает по коррел€ции значение
    res.InitProp p_res_atma, pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    IPR_p_wf_atma = res.calc_p_wf_atma(q_liq_sm3day)
    Set res = Nothing
    
    Exit Function
err1:
    IPR_p_wf_atma = -1
    add_log_msg "Error:IPR_p_wf_atma:" & Err.Description
End Function
'description_to_manual      - дл€ автогенерации описани€ - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет коэффициента продуктивности пласта
' по данным тестовой эксплуатации
Public Function IPR_pi_sm3dayatm( _
                 ByVal Qtest_sm3day As Double, _
                 ByVal pwf_test_atma As Double, _
                 ByVal p_res_atma As Double, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1)
' Qtest_sm3day   - тестовый дебит скважины, ст.м3/сут
' pwf_test_atma  - тестовое забойное давление, абс. атм
' p_res_atma      - пластовое давление, абс. атм
' fw_perc        - обводненность, %
' pb_atma        - давление насыщени€, абс. атм
' результат      - значение коэффициента продуктивности, ст.м3/сут/атм
'description_end
On Error GoTo err1:
    Dim res As New CReservoirVogel
    If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид подсчитает по коррел€ции значение
    res.InitProp p_res_atma, pb_atma, fw_perc
    IPR_pi_sm3dayatm = res.calc_pi_sm3dayatm(Qtest_sm3day, pwf_test_atma)
    Set res = Nothing
    
    Exit Function
err1:
    IPR_pi_sm3dayatm = -1
    add_log_msg "error in function :IPR_pi_sm3dayatm:" & Err.Description
End Function

