'=======================================================================================
'Unifloc 7.35 new year                                khabibullinra@gmail.com
'Petroleum engineering calculations modules (macroses)
'2000 - 2021
'
'=======================================================================================
' базовые функции для проведения расчетов из интерфейса Excel
Option Explicit
' ==============  функции для расчета пласта ==========================
' =====================================================================
'description_to_manual      - для автогенерации
' расчет дебита по давлению и продуктивности
Public Function IPR_q_liq_sm3day( _
                 ByVal pi_sm3dayatm As Double, _
                 ByVal p_res_atma As Double, _
                 ByVal p_wf_atma, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1, _
        Optional ByVal t_res_C As Double = 80)
' pi_sm3dayatm   - коэффициент продуктивности, ст.м3/сут/атм
' p_res_atma     - пластовое давление, абс. атм
' p_wf_atma      - забойное давление, абс. атм.
'                  можно задать список [],
'                  тогда результат будет в виде списка
' fw_perc        - обводненность, %
' pb_atma        - давление насыщения, абс. атм
' t_res_C        - температура флюида в пласте, С
' результат      - значение дебита жидкости, ст.м3/сут
'                  или список значений
'description_end
On Error GoTo err1:
    Dim cl As Collection
    Dim cl_q_out As New Collection
    Dim cl_p_out As New Collection
    Dim cl_t_out As New Collection
    Dim dic_out As New Dictionary
    
    Dim pwf
    Dim res As New CReservoirVogel
    If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид додсчитает по корреляции значение
    res.InitProp p_res_atma, pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    
    ' разберемся со значением забойного давления
    
    Select Case VBA.VarType(p_wf_atma)
        Case vbDouble
             IPR_q_liq_sm3day = res.calc_q_liq_sm3day(p_wf_atma)
        Case vbString
            Select Case VBA.Mid$(p_wf_atma, 1, 1)
                Case "["
                    Set cl = ParseJson(p_wf_atma)
                    For Each pwf In cl
                        cl_q_out.Add res.calc_q_liq_sm3day(pwf)
                        cl_p_out.Add pwf
                        cl_t_out.Add t_res_C
                    Next
                    Set dic_out("p_atma") = cl_p_out
                    Set dic_out("t_C") = cl_t_out
                    Set dic_out("q_liq_sm3day") = cl_q_out
                    IPR_q_liq_sm3day = ConvertToJson(dic_out)
                Case Else
                    pwf = Cdbl_(p_wf_atma)
                    IPR_q_liq_sm3day = res.calc_q_liq_sm3day(pwf)
            End Select
    End Select
        
    Set res = Nothing
    
    Exit Function
err1:
    IPR_q_liq_sm3day = "Error:IPR_q_liq_sm3day:" & Err.Description
End Function
'description_to_manual      - для автогенерации
' расчет забойного давления по дебиту и продуктивности
Public Function IPR_p_wf_atma( _
                 ByVal pi_sm3dayatm As Double, _
                 ByVal p_res_atma As Double, _
                 ByVal q_liq_sm3day, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1, _
        Optional ByVal t_res_C As Double = 80)
' pi_sm3dayatm   - коэффициент продуктивности, ст.м3/сут/атм
' p_res_atma      - пластовое давление, абс. атм
' q_liq_sm3day    - дебит жидкости скважины на поверхности, ст.м3/сут
' fw_perc        - обводненность, %
' pb_atma        - давление насыщения, абс. атм
' t_res_C        - температура флюида в пласте, С
' результат      - значение забойного давления, абс. атм
'description_end
On Error GoTo err1:
    Dim cl As Collection
    Dim cl_q_out As New Collection
    Dim cl_p_out As New Collection
    Dim cl_t_out As New Collection
    Dim dic_out As New Dictionary
    Dim ql
    Dim res As New CReservoirVogel
    If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид подсчитает по корреляции значение
    res.InitProp p_res_atma, pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    
    
    ' разберемся со значением дебита
    
    Select Case VBA.VarType(q_liq_sm3day)
        Case vbDouble
             IPR_p_wf_atma = res.calc_p_wf_atma(q_liq_sm3day)
        Case vbString
            Select Case VBA.Mid$(q_liq_sm3day, 1, 1)
                Case "["
                    Set cl = ParseJson(q_liq_sm3day)
                    For Each ql In cl
                        cl_q_out.Add ql
                        cl_p_out.Add res.calc_p_wf_atma(ql)
                        cl_t_out.Add t_res_C
                    Next
                    Set dic_out("p_atma") = cl_p_out
                    Set dic_out("t_C") = cl_t_out
                    Set dic_out("q_liq_sm3day") = cl_q_out
                    IPR_p_wf_atma = ConvertToJson(dic_out)
                Case Else
                    ql = Cdbl_(q_liq_sm3day)
                    IPR_p_wf_atma = res.calc_p_wf_atma(ql)
            End Select
    End Select
    
    Set res = Nothing
    
    Exit Function
err1:
    IPR_p_wf_atma = "Error:IPR_p_wf_atma:" & Err.Description
   
End Function
'description_to_manual      - для автогенерации
' расчет коэффициента продуктивности пласта
' по данным тестовой эксплуатации
Public Function IPR_pi_sm3dayatm( _
                 ByVal Qtest_sm3day As Double, _
                 ByVal pwf_test_atma As Double, _
                 ByVal p_res_atma As Double, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1)
' Qtest_sm3day   - тестовый дебит скважины, ст.м3/сут
' pwf_test_atma  - тестовое забойное давление, абс. атм
' p_res_atma      - пластовое давление, абс. атм
' fw_perc        - обводненность, %
' pb_atma        - давление насыщения, абс. атм
' результат      - значение коэффициента продуктивности, ст.м3/сут/атм
'description_end
On Error GoTo err1:
    Dim res As New CReservoirVogel
    If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид подсчитает по корреляции значение
    res.InitProp p_res_atma, pb_atma, fw_perc
    IPR_pi_sm3dayatm = res.calc_pi_sm3dayatm(Qtest_sm3day, pwf_test_atma)
    Set res = Nothing
    
    Exit Function
err1:
    IPR_pi_sm3dayatm = -1
    add_log_msg "error in function :IPR_pi_sm3dayatm:" & Err.Description
End Function
'description_to_manual      - для автогенерации
' расчет кривой IPR в формате json
' pq_crv - кривая давление температура
Public Function IPR_pq_crv( _
                 ByVal pi_sm3dayatm As Double, _
                 ByVal p_res_atma As Double, _
        Optional ByVal fw_perc As Double = 0, _
        Optional ByVal pb_atma As Double = -1, _
        Optional param As String = "")
' pi_sm3dayatm   - коэффициент продуктивности, ст.м3/сут/атм
' p_res_atma     - пластовое давление, абс. атм
' fw_perc        - обводненность, %
' pb_atma        - давление насыщения, абс. атм
' param          - дополнительные параметры построения
' результат      - кривая IPR в формате json.
'        можно использовать decode_json_crv для декодирования
'description_end
Dim i As Integer
Dim num As Integer
Dim crv_IPR As New CInterpolation
Dim pwf_i As Double
Dim qliq_i As Double
Dim res As New CReservoirVogel
Dim prmd As Dictionary
On Error GoTo err1:
    Set prmd = param_to_dict(param)
    
    num = prmd(prm_num_value) - 1
    If num <= 1 Then
        num = 19
    End If
    
    For i = 0 To num
        pwf_i = 1 + (p_res_atma - 1) / num * i
        
        If pb_atma <= 0 Then pb_atma = 0   ' поставим ноль иначе флюид подсчитает по корреляции значение
        res.InitProp p_res_atma, pb_atma, fw_perc
        res.pi_sm3dayatm = pi_sm3dayatm
        qliq_i = res.calc_q_liq_sm3day(pwf_i)
        
        crv_IPR.AddPoint pwf_i, qliq_i
        
    Next i
    crv_IPR.xName = "p_wf_atma"
    crv_IPR.yName = "q_liq_sm3day"
    
    IPR_pq_crv = ConvertToJson(crv_IPR.getDict)
    Exit Function
err1:
    IPR_pq_crv = -1
    add_log_msg "error in function :IPR_pq_crv:" & Err.Description
End Function

