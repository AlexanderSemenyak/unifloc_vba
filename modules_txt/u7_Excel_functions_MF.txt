'=======================================================================================
'Unifloc 7.26  coronav2                                     khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2020
'
'=======================================================================================
'функции для проведения расчетов из интерфейса Excel
'многофазный поток в трубах и элементах инфраструктуры
Option Explicit
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' функция расчета коэффициента Джоуля Томсона
'Public Function MF_CJT_Katm( _
'             ByVal p_atma As Double, _
'             ByVal t_C As Double, _
'    Optional ByVal str_PVT As String = PVT_DEFAULT, _
'    Optional ByVal q_liq_sm3day As Double = 10, _
'    Optional ByVal fw_perc As Double = 0)
'' p_atma      - давление, атм
'' t_C         - температура, С.
'' str_PVT     - encoded to string PVT properties of fluid
'' q_liq_sm3day - liquid rate (at surface)
'' fw_perc     - water fraction (watercut)
'' output - number
''description_end
'
'
'On Error GoTo err1:
'    Dim PVT As New CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.q_liq_sm3day = q_liq_sm3day
'    PVT.fw_perc = fw_perc
'    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
'    MF_CJT_Katm = PVT.cJT_Katm
'    Exit Function
'err1:
'    MF_CJT_Katm = -1
'    addLogMsg "Error:MF_CJT_Katm:" & Err.Description
'End Function
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' расчет объемного расхода газожидкостной смеси
'' для заданных термобарических условий
'Public Function MF_q_mix_rc_m3day( _
'             ByVal q_liq_sm3day As Double, _
'             ByVal fw_perc As Double, _
'             ByVal p_atma As Double, _
'             ByVal t_C As Double, _
'    Optional ByVal str_PVT As String = PVT_DEFAULT)
'' q_liq_sm3day- дебит жидкости на поверхности
'' fw_perc    - объемная обводненность
'' p_atma     - давление, атм
'' t_C        - температура, С.
'' str_PVT    - закодированная строка с параметрами PVT.
''              если задана - перекрывает другие значения
'' результат  - число - расход ГЖС, м3/сут.
''description_end
'
'On Error GoTo err1:
'    Dim PVT As CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.fw_perc = fw_perc
'    PVT.q_liq_sm3day = q_liq_sm3day
'    Call PVT.calc_PVT(p_atma, t_C)
'    MF_q_mix_rc_m3day = PVT.q_mix_rc_m3day
'    Exit Function
'err1:
'    MF_q_mix_rc_m3day = -1
'    addLogMsg "Error:MF_q_mix_rc_m3day:" & Err.Description
'End Function
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' расчет плотности газожидкостной смеси для заданных  условий
'Public Function MF_rho_mix_kgm3( _
'             ByVal q_liq_sm3day As Double, _
'             ByVal fw_perc As Double, _
'             ByVal p_atma As Double, _
'             ByVal t_C As Double, _
'    Optional ByVal str_PVT As String = PVT_DEFAULT)
'' q_liq_sm3day- дебит жидкости на поверхности
'' fw_perc    - объемная обводненность
'' p_atma     - давление, атм
'' t_C        - температура, С.
'' str_PVT    - закодированная строка с параметрами PVT.
''              если задана - перекрывает другие значения
'' результат  - число - плотность ГЖС, кг/м3.
''description_end
'
'On Error GoTo err1:
'    Dim PVT As CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.fw_perc = fw_perc
'    PVT.q_liq_sm3day = q_liq_sm3day
'    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
'    MF_rho_mix_kgm3 = PVT.rho_mix_rc_kgm3
'    Exit Function
'err1:
'    MF_rho_mix_kgm3 = -1
'    addLogMsg "Error:MF_rho_mix_kgm3:" & Err.Description
'
'End Function
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' расчет вязкости газожидкостной смеси
'' для заданных термобарических условий
'Public Function MF_mu_mix_cP( _
'            ByVal q_liq_sm3day As Double, _
'            ByVal fw_perc As Double, _
'            ByVal p_atma As Double, _
'            ByVal t_C As Double, _
'   Optional ByVal str_PVT As String = PVT_DEFAULT)
'' q_liq_sm3day - дебит жидкости на поверхности
'' fw_perc     - объемная обводненность
'' p_atma      - давление, атм
'' t_C         - температура, С.
'' str_PVT     - закодированная строка с параметрами PVT.
''              если задана - перекрывает другие значения
'' результат   - число - вязкость ГЖС, м3/сут.
''description_end
'
'On Error GoTo err1:
'    Dim PVT As CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.fw_perc = fw_perc
'    PVT.q_liq_sm3day = q_liq_sm3day
'    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
'    MF_mu_mix_cP = PVT.mu_mix_cP
'    Exit Function
'err1:
'    MF_mu_mix_cP = -1
'    addLogMsg "Error:MF_mu_mix_cP:" & Err.Description
'End Function
'
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' расчет доли газа в потоке
'Public Function MF_gas_fraction_d( _
'              ByVal p_atma As Double, _
'              ByVal t_C As Double, _
'     Optional ByVal fw_perc = 0, _
'     Optional ByVal str_PVT As String = PVT_DEFAULT, _
'     Optional ByVal ksep_add_fr As Double = 0)
'' p_atma   - давление, атм
'' t_C      - температура, С.
'' fw_perc  - обводненность объемная
'' str_PVT  - закодированная строка с параметрами PVT.
''            если задана - перекрывает другие значения
'' ksep_add_fr - коэффициент сепарации дополнительный
''           для сепарации заданной в потоке. применяется
''           для сепарации при искомом давлении
'' результат - число - доля газа в потоке
''              (расходная без проскальзования)
''description_end
'On Error GoTo err1:
'    Dim PVT As CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.fw_perc = fw_perc
'    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
'    MF_gas_fraction_d = PVT.gas_fraction_d(ksep_add_fr)
'    Exit Function
'err1:
'    MF_gas_fraction_d = -1
'    addLogMsg "Error:MF_gas_fraction_d:" & Err.Description
'End Function
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' расчет давления при котором
'' достигается заданная доля газа в потоке
'Public Function MF_p_gas_fraction_atma( _
'               ByVal free_gas_d As Double, _
'               ByVal t_C As Double, _
'               ByVal fw_perc As Double, _
'      Optional ByVal str_PVT As String = PVT_DEFAULT, _
'      Optional ByVal ksep_add_fr As Double = 0)
'' free_gas_d - допустимая доля газа в потоке;
'' t_C        - температура, С;
'' fw_perc    - объемная обводненность, проценты %;
'' str_PVT    - закодированная строка с параметрами PVT.
''              Если задана - перекрывает другие значения.
'' ksep_add_fr - коэффициент сепарации дополнительный
''           для сепарации заданной в потоке. применяется
''           для сепарации при искомом давлении
'' результат  - число - давление, атма.
''description_end
'On Error GoTo err1:
'    Dim PVT As CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.fw_perc = fw_perc
'    MF_p_gas_fraction_atma = PVT.p_gas_fraction_atma(free_gas_d, t_C, ksep_add_fr)
'    Exit Function
'err1:
'    MF_p_gas_fraction_atma = -1
'    addLogMsg "Error:MF_p_gas_fraction_atma:" & Err.Description
'End Function
'
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'' расчет газового фактора
'' при котором достигается заданная доля газа в потоке
'Public Function MF_rp_gas_fraction_m3m3( _
'                ByVal free_gas_d As Double, _
'                ByVal p_atma As Double, _
'                ByVal t_C As Double, _
'                ByVal fw_perc As Double, _
'       Optional ByVal str_PVT As String = PVT_DEFAULT, _
'       Optional ByVal Rp_limit_m3m3 As Double = 500, _
'       Optional ByVal ksep_add_fr As Double = 0)
'' free_gas_d - допустимая доля газа в потоке
'' p_atma     - давление, атм
'' t_C        - температура, С.
'' fw_perc    - объемная обводненность, проценты %;
'' str_PVT    - закодированная строка с параметрами PVT.
''              если задана - перекрывает другие значения
'' Rp_limit_m3m3 - верхняя граница оценки ГФ
'' ksep_add_fr - коэффициент сепарации дополнительный
''           для сепарации заданной в потоке. применяется
''           для сепарации при искомом давлении
'' результат  - число - газовый фактор, м3/м3.
''description_end
'On Error GoTo err1:
'    Dim PVT As CPVT
'    Call PVT.init_prop(str_PVT)
'    PVT.fw_perc = fw_perc
'    MF_rp_gas_fraction_m3m3 = PVT.rp_gas_fraction_m3m3(free_gas_d, p_atma, t_C, ksep_add_fr, Rp_limit_m3m3)
'    Exit Function
'err1:
'    MF_rp_gas_fraction_m3m3 = -1
'    addLogMsg "Error:MF_rp_gas_fraction_m3m3:" & Err.Description
'End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет натуральной сепарации газа на приеме насоса
Public Function MF_ksep_natural_d( _
             ByVal q_liq_sm3day As Double, _
             ByVal fw_perc As Double, _
             ByVal p_intake_atma As Double, _
    Optional ByVal t_intake_C As Double = 50, _
    Optional ByVal d_intake_mm As Double = 90, _
    Optional ByVal d_cas_mm As Double = 120, _
    Optional ByVal str_PVT As String = PVT_DEFAULT)
' q_liq_sm3day   - дебит жидкости в поверхностных условиях
' fw_perc       - обводненность
' p_intake_atma - давление сепарации
' t_intake_C    - температура сепарации
' d_intake_mm   - диаметр приемной сетки
' d_cas_mm      - диаметр эксплуатационной колонны
' str_PVT       - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' результат     - число - естественная сепарация
'description_end
On Error GoTo err1:
    Dim fluid As New CPVT
    Call fluid.init_feed(str_PVT)
    fluid.q_liq_sm3day = q_liq_sm3day
    fluid.fw_perc = fw_perc
    Call fluid.calc_PVT(p_intake_atma, t_intake_C)
    With fluid
        MF_ksep_natural_d = unf_natural_separation(d_intake_mm / 1000, d_cas_mm / 1000, .q_liq_sm3day, .q_gas_sm3day, .bo_m3m3, .bg_m3m3, _
                                                    .sigma_oil_gas_Nm, .sigma_wat_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
    End With
    Exit Function
err1:
    MF_ksep_natural_d = -1
    addLogMsg "Error:MF_ksep_natural_d:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет общей сепарации на приеме насоса
Public Function MF_ksep_total_d( _
        ByVal SepNat As Double, _
        ByVal SepGasSep As Double)
' SepNat        - естественная сепарация
' SepGasSep     - искусственная сепарация (газосепаратор)
    MF_ksep_total_d = SepNat + (1 - SepNat) * SepGasSep
End Function
'description_end
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'расчет градиента давления
'с использованием многофазных корреляций
Public Function MF_dpdl_atmm(ByVal d_m As Double, _
             ByVal p_atma As Double, _
             ByVal Ql_rc_m3day As Double, _
             ByVal Qg_rc_m3day As Double, _
    Optional ByVal mu_oil_cP As Double = const_mu_o, _
    Optional ByVal mu_gas_cP As Double = const_mu_g, _
    Optional ByVal sigma_oil_gas_Nm As Double = const_sigma_oil_Nm, _
    Optional ByVal rho_lrc_kgm3 As Double = const_go_ * 1000, _
    Optional ByVal rho_grc_kgm3 As Double = const_gg_ * const_rho_air, _
    Optional ByVal eps_m As Double = 0.0001, _
    Optional ByVal theta_deg As Double = 90, _
    Optional ByVal hcorr As Integer = 1, _
    Optional ByVal param_out As Integer = 0, _
    Optional ByVal c_calibr_grav As Double = 1, _
    Optional ByVal c_calibr_fric As Double = 1)
' расчет градиента давления по одной из корреляций
' d_m - диаметр трубы в которой идет поток
' p_atma - давление в точке расчета
' Ql_rc_m3day - дебит жидкости в рабочих условиях
' Qg_rc_m3day - дебит газа в рабочих условиях
' mu_oil_cP - вязкость нефти в рабочих условиях
' mu_gas_cP - вязкость газа в рабочих условиях
' sigma_oil_gas_Nm - поверхностное натяжение
'              жидкость газ
' rho_lrc_kgm3 - плотность нефти
' rho_grc_kgm3 - плотность газа
' eps_m     - шероховатость
' theta_deg - угол от горизонтали
' hcorr  - тип корреляции
' param_out - параметр для вывода
' c_calibr_grav - калибровка гравитации
' c_calibr_fric - калибровка трения
'description_end
    
    Dim PrGrad
    
On Error GoTo er1:
    Select Case hcorr
        Case 0
        
                PrGrad = unf_BegsBrillGradient(d_m, theta_deg, eps_m, _
                                Ql_rc_m3day, Qg_rc_m3day, _
                                mu_oil_cP, mu_gas_cP, _
                                sigma_oil_gas_Nm, _
                                rho_lrc_kgm3, _
                                rho_grc_kgm3, , , c_calibr_grav, c_calibr_fric)
        Case 1
        
                PrGrad = unf_AnsariGradient(d_m, theta_deg, eps_m, _
                                Ql_rc_m3day, Qg_rc_m3day, _
                                mu_oil_cP, mu_gas_cP, _
                                sigma_oil_gas_Nm, _
                                rho_lrc_kgm3, _
                                rho_grc_kgm3, _
                                p_atma, c_calibr_grav, c_calibr_fric)
       Case 2
        
                PrGrad = unf_UnifiedTUFFPGradient(d_m, theta_deg, eps_m, _
                                Ql_rc_m3day, Qg_rc_m3day, _
                                mu_oil_cP, mu_gas_cP, _
                                sigma_oil_gas_Nm, _
                                rho_lrc_kgm3, _
                                rho_grc_kgm3, _
                                p_atma, c_calibr_grav, c_calibr_fric)
       Case 3
        
                PrGrad = unf_GrayModifiedGradient(d_m, theta_deg, eps_m, _
                                Ql_rc_m3day, Qg_rc_m3day, _
                                mu_oil_cP, mu_gas_cP, _
                                sigma_oil_gas_Nm, _
                                rho_lrc_kgm3, _
                                rho_grc_kgm3, _
                                , , , c_calibr_grav, c_calibr_fric)
       Case 4
        
                PrGrad = unf_HagedornandBrawnmodified(d_m, theta_deg, eps_m, _
                                Ql_rc_m3day, Qg_rc_m3day, _
                                mu_oil_cP, mu_gas_cP, _
                                sigma_oil_gas_Nm, _
                                rho_lrc_kgm3, _
                                rho_grc_kgm3, _
                                p_atma, , , , c_calibr_grav, c_calibr_fric)
    End Select
    
    If param_out = 0 Then
        MF_dpdl_atmm = PrGrad
    Else
        MF_dpdl_atmm = PrGrad(param_out)
    End If
    Exit Function
er1:
    MF_dpdl_atmm = -1
    addLogMsg "Error:MF_dpdl_atmm:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'  подбор параметров потока через трубу при известном
'  перепаде давления с использованием многофазных корреляций
Public Function MF_calibr_pipeline( _
                 ByVal p_calc_from_atma As Double, _
                 ByVal p_calc_to_atma As Double, _
             ByVal t_calc_from_C As Double, _
             ByVal t_val, _
             ByVal h_list_m As Variant, _
             ByVal diam_list_mm As Variant, _
             ByVal q_liq_sm3day As Double, _
             ByVal fw_perc As Double, _
    Optional ByVal q_gas_sm3day As Double = 0, _
    Optional ByVal str_PVT As String = PVT_DEFAULT, _
    Optional ByVal calc_flow_direction As Integer = 11, _
    Optional ByVal hydr_corr As H_CORRELATION = 0, _
    Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
    Optional ByVal c_calibr = 1, _
    Optional ByVal roughness_m As Double = 0.0001, _
    Optional ByVal out_curves As Integer = 1, _
    Optional ByVal out_curves_num_points As Integer = 20, _
    Optional ByVal calibr_type As Integer = 0)
'p_calc_from_atma - давление начальное, атм
'                   граничное значение для проведения расчета
'p_calc_to_atma   - давление конечное, атм
'                   граничное значение для проведения расчета
' t_calc_from_C - температура в точке где задано давление расчета
' t_val     - температура вдоль трубопровода
'           если число то температура на другом конце трубы
'           если range или таблица [0..N,0..1] то температура
'           окружающей среды по вертикальной глубине, С
' h_list_m  - траектория трубопровода, если число то измеренная
'           длина, range или таблица [0..N,0..1] то траектория
' diam_list_mm  - внутрнний диаметр трубы, если число то задается
'           постоянный диаметр, если range или таблица [0..N,0..1]
'           то задается зависимость диаметра от измеренной длины
' q_liq_sm3day - дебит жидкости в поверхностных условиях, нм3/сут
'           если q_liq_sm3day =0 и q_gas_sm3day > 0
'           тогда считается барботаж газа через жидкость
' fw_perc   - обводненность объемная в стандартных условиях
' q_gas_sm3day  - свободный газ нм3/сут. дополнительный к PVT потоку.
'           учитывается для барботажа или режима потока газа
'           в других случаях добавляется к общему потоку меняя rp
' str_PVT   - закодированная строка с параметрами PVT.
'           если задана - перекрывает другие значения
'           если задан флаг gas_only = 1 то жидкость не учитывается
' calc_flow_direction - направление расчета и потока относительно
'           координат.  11 расчет и поток по координате
'                       10 расчет по коордиате, поток против
'                       00 расчет и поток против координате
'                       01 расчет против координат, поток по
' hydr_corr   - гидравлическая корреляция, H_CORRELATION
'           BeggsBrill = 0,
'           Ansari = 1,
'           Unified = 2,
'           Gray = 3,
'           HagedornBrown = 4,
'           SakharovMokhov = 5
' temp_method  - метод расчета температуры
'           0 - линейное распределение по длине
'           1 - температура равна температуре окружающей среды
'           2 - расчет температуры с учетом эмиссии в окр. среду
' c_calibr  - поправка на гравитационную составляющую
'           перепада давления, если дать ссылку на две ячейки,
'           то вторая будет поправка на трение.
' roughness_m - шероховатость трубы
' out_curves - флаг вывод значений между концами трубы
'           1 основные, 2 все значения.
'           вывод может замедлять расчет (не сильно)
' out_curves_num_points - количество точек для вывода значений
'           между концами трубы.
' calibr_type - тип калибровки
'          0 - подбор параметра c_calibr_grav
'          1 - подбор параметра c_calibr_fric
'          2 - подбор газового фактор
'          3 - подбор обводненности
' результат   - массив с подобранным парамером и подробностями.
'description_end
    Dim pipe As New CPipe
    Dim res(), res1()
    Dim CoeffA(0 To 2)
    Dim Func As String
    Dim cal_type_string As String
    Dim val_min As Double, val_max As Double
    Dim out, out_desc
    Dim prm As New CSolveParam
    
On Error GoTo err1:
    
    Set pipe = new_pipeline_with_stream(q_liq_sm3day, _
                                        fw_perc, _
                                        h_list_m, _
                                        t_calc_from_C, _
                                        calc_flow_direction, _
                                        str_PVT, _
                                        diam_list_mm, _
                                        hydr_corr, _
                                        t_val, _
                                        temp_method, _
                                        c_calibr, _
                                        roughness_m, _
                                        q_gas_sm3day)
    
    ' prepare solution function
    Set CoeffA(0) = pipe
        CoeffA(1) = p_calc_from_atma
        CoeffA(2) = p_calc_to_atma
        
    Select Case calibr_type
        Case 0
            Func = "calc_pipe_dp_error_calibr_grav_atm"
            cal_type_string = "calibr_grav"
            val_min = 0.5
            val_max = 1.5
        Case 1
            Func = "calc_pipe_dp_error_calibr_fric_atm"
            cal_type_string = "calibr_fric"
            val_min = 0.1
            val_max = 1
        Case 2
            Func = "calc_pipe_dp_error_rp_atm"
            cal_type_string = "rp"
            val_min = 20 'pipe.fluid.rp_m3m3 * 0.5
            val_max = pipe.fluid.rp_m3m3 * 2
            ' Расширить диапазон поиска по газовому фактору может быть опасно
            ' так как возможна неоднозначность решения
            ' а текущий метод поиска работает только если есть одно решение
        Case 3
            Func = "calc_pipe_dp_error_fw_atm"
            cal_type_string = "fw"
            val_min = 0
            val_max = 1
            If val_max > 1 Then val_max = 1
        Case 4
            Func = "calc_pipe_dp_error_qliq_atm"
            cal_type_string = "qliq"
            val_min = 0
            val_max = pipe.fluid.q_liq_sm3day * 1.5
        Case 5
            Func = "calc_pipe_dp_error_qgas_atm"
            cal_type_string = "qgas"
            val_min = 0
            If q_gas_sm3day > 0 Then
                val_max = q_gas_sm3day * 2
            Else
                val_max = 10000
            End If
        Case Else
            ' solve_equation_bisection without initialasing func crashes excel
            MF_calibr_pipeline = "not implemented"
            Exit Function
    End Select
    
    prm.y_tolerance = const_pressure_tolerance
    If solve_equation_bisection(Func, val_min, val_max, CoeffA, prm) Then
    
        out = Array(prm.x_solution, _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    Else
        out = Array("no solution", _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    End If
        
    out_desc = Array("solution", _
                     "cal_type", _
                     "y_solution", _
                     "iterations", _
                     "description")
    MF_calibr_pipeline = array_join(Array(out, out_desc))
    Exit Function
err1:
    MF_calibr_pipeline = Array(-1, "error")
    addLogMsg "Error:MF_calibr_pipeline:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'  подбор параметров потока через трубу при известном
'  перепаде давления с использованием многофазных корреляций
' (лучше не использовать - используйте MF_calibr_pipeline)
Public Function MF_calibr_pipe( _
        ByVal p_calc_from_atma As Double, _
        ByVal p_calc_to_atma As Double, _
        ByVal t_calc_from_C As Double, _
        ByVal t_calc_to_C As Double, _
        ByVal length_m As Double, _
        ByVal theta_deg As Double, _
        ByVal d_mm As Double, _
        ByVal q_liq_sm3day As Double, _
        ByVal fw_perc As Double, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal calc_flow_direction As Integer = 11, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal c_calibr = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal calibr_type As Integer = 0)
'p_calc_from_atma - давление с которого начинается расчет, атм
'                   граничное значение для проведения расчета
' t_calc_from_C   - температура в точке где задано давление, С
' t_calc_to_C     - температура на другом конце трубы
'                 по умолчанию температура вдоль трубы постоянна
'                   если задано то меняется линейно по трубе
' length_m        - Длина трубы, измеренная, м
' theta_deg       - угол направления потока к горизонтали
' d_mm            - внутренний диаметр трубы
' q_liq_sm3day     - дебит жидкости в поверхностных условиях
'              если q_liq_sm3day =0 и q_gas_sm3day > 0
'              тогда считается барботаж газа через жидкость
' fw_perc         - обводненность
' q_gas_sm3day    - свободный газ. дополнительный к PVT потоку.
' str_PVT         - закодированная строка с параметрами PVT.
'                   если задана - перекрывает другие значения
'        если задан флаг gas_only = 1 то жидкость не учитывается
' calc_flow_direction - направление расчета и потока
'                   относительно координат
'                   если = 11 расчет и поток по координате
'                   если = 10 расчет по, поток против координат
'                   если = 00 расчет и поток против координате
'                   если = 01 расчет против, поток по координате
' hydr_corr       - гидравлическая корреляция, H_CORRELATION
'                    BeggsBrill = 0
'                    Ansari = 1
'                    Unified = 2
'                    Gray = 3
'                    HagedornBrown = 4
'                    SakharovMokhov = 5
' c_calibr        - поправка на гравитационную составляющую
'           перепада давления, если дать ссылку на две ячейки,
'           то вторая будет поправка на трение
' roughness_m     - шероховатость трубы
' out_curves      - флаг вывод значений между концами трубы
'                   0 минимум, 1 основные, 2 все значения.
'                   вывод может замедлять расчет (не сильно)
' out_curves_num_points - количество точек для вывода значений
'                   между концами трубы.
' результат       - число - давление на другом конце трубы atma.
'                  или массив - первая строка значения
'                               вторая строка - подписи
'description_end
    
    
    Dim pipe As New CPipe
    Dim prm As New CSolveParam
    Dim CoeffA(0 To 2)
    Dim Func As String
    Dim cal_type_string As String
    Dim val_min As Double, val_max As Double
    Dim out, out_desc
On Error GoTo err1:
    ' check pipe length
    length_m = Abs(length_m)                ' length must be positive
    If length_m = 0 Then
        MF_calibr_pipe = Array(p_calc_from_atma, t_calc_from_C)
        Exit Function
    End If
    ' initialize pipe
    Set pipe = new_pipe_with_stream(q_liq_sm3day, fw_perc, length_m, calc_flow_direction, _
                                    str_PVT, theta_deg, d_mm, hydr_corr, _
                                    t_calc_from_C, t_calc_to_C, _
                                    c_calibr, _
                                    roughness_m, q_gas_sm3day)
    ' prepare solution function
    Set CoeffA(0) = pipe
        CoeffA(1) = p_calc_from_atma
        CoeffA(2) = p_calc_to_atma
        
    Select Case calibr_type
        Case 0
            Func = "calc_pipe_dp_error_calibr_grav_atm"
            cal_type_string = "calibr_grav"
            val_min = 0.5
            val_max = 1.5
        Case 1
            Func = "calc_pipe_dp_error_calibr_fric_atm"
            cal_type_string = "calibr_fric"
            val_min = 0.5
            val_max = 1.5
        Case 2
            Func = "calc_pipe_dp_error_rp_atm"
            cal_type_string = "rp"
            val_min = 20 'pipe.fluid.rp_m3m3 * 0.5
            val_max = pipe.fluid.rp_m3m3 * 2
            ' Расширить диапазон поиска по газовому фактору может быть опасно
            ' так как возможна неоднозначность решения
            ' а текущий метод поиска работает только если есть одно решение
        Case 3
            Func = "calc_pipe_dp_error_fw_atm"
            cal_type_string = "fw"
            val_min = 0
            val_max = 1
            If val_max > 1 Then val_max = 1
        Case 4
            Func = "calc_pipe_dp_error_qliq_atm"
            cal_type_string = "qliq"
            val_min = 1
            val_max = pipe.fluid.q_liq_sm3day * 1.5
        Case 5
            Func = "calc_pipe_dp_error_qgas_atm"
            cal_type_string = "qgas"
            val_min = 0
            If q_gas_sm3day > 0 Then
                val_max = q_gas_sm3day * 2
            Else
                val_max = 10000
            End If
        Case Else
            ' solve_equation_bisection without initialasing func crashes excel
            MF_calibr_pipe = "not implemented"
            Exit Function
    End Select
    
    prm.y_tolerance = const_pressure_tolerance
    If solve_equation_bisection(Func, val_min, val_max, CoeffA, prm) Then
    
        out = Array(prm.x_solution, _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    Else
        out = Array("no solution", _
                    cal_type_string, _
                    CStr(prm.y_solution), _
                    CStr(prm.iterations), _
                    prm.msg)
    End If
        
    out_desc = Array("solution", _
                     "cal_type", _
                     "y_solution", _
                     "iterations", _
                     "description")
    MF_calibr_pipe = array_join(Array(out, out_desc))
    
    Exit Function
err1:
    MF_calibr_pipe = Array(-1, "error")
    addLogMsg "Error:MF_calibr_pipe:" & Err.Description
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет корректирующего фактора (множителя) модели штуцера под замеры
' медленный расчет - калибровка подбирается
Public Function MF_calibr_choke( _
            ByVal q_liq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal d_choke_mm As Double, _
            Optional ByVal p_in_atma As Double = -1, _
            Optional ByVal p_out_atma As Double = -1, _
            Optional ByVal d_pipe_mm As Double = 70, _
            Optional ByVal t_choke_C As Double = 20, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal q_gas_sm3day As Double = 0, _
            Optional ByVal calibr_type As Integer = 0)
            
' q_liq_sm3day   - дебит жидкости в ст. условиях
' fw_perc       - обводненность
' d_choke_mm    - диаметр штуцера (эффективный), мм
' p_in_atma     - давление на входе (высокой стороне)
' p_out_atma    - давление на выходе (низкой стороне)
' d_pipe_mm     - диаметр трубы до и после штуцера, мм
' t_choke_C     - температура, С.
' str_PVT       - закодированная строка с параметрами PVT,
'                 если задана - перекрывает другие значения
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' calibr_type - тип калибровки
'             0 - подбор параметра c_calibr
'             1 - подбор диаметра штуцера
'             2 - подбор газового фактор
'             3 - подбор обводненности
'             4 - подбор дебита жидкости
'             5 - подбор дебита газа свободного
' результат     - число - калибровочный коэффициент для модели.
'                 штуцера  - множитель на дебит через штуцер
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim PT As PTtype
    Dim PVT As CPVT
    Dim out, out_desc
    Dim CoeffA(0 To 2)
    Dim Func As String
    Dim cal_type_string As String
    Dim val_min As Double, val_max As Double
    Dim prm As New CSolveParam
    
    Call PVT.init_feed(str_PVT)
    
    If PVT.gas_only Then
        MF_calibr_choke = "not implemented yet"
        Exit Function
    End If
    
    PVT.q_gas_free_sm3day = q_gas_sm3day
    Set choke.fluid = PVT
    choke.fluid.q_liq_sm3day = q_liq_sm3day
    choke.fluid.fw_perc = fw_perc
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    choke.t_choke_C = t_choke_C
    
     ' prepare solution function
    Set CoeffA(0) = choke
        CoeffA(1) = p_in_atma
        CoeffA(2) = p_out_atma
        
    Select Case calibr_type
        Case 0
            Func = "calc_choke_dp_error_calibr_grav_atm"
            cal_type_string = "calibr"
            val_min = 0.5
            val_max = 1.5
        Case 1
            Func = "calc_choke_dp_error_diam_atm"
            cal_type_string = "diam_choke"
            val_min = choke.d_choke_m / 2
            val_max = choke.d_up_m
        Case 2
            Func = "calc_choke_dp_error_rp_atm"
            cal_type_string = "rp"
            val_min = 20 'pipe.fluid.rp_m3m3 * 0.5
            val_max = choke.fluid.rp_m3m3 * 2
            ' Расширить диапазон поиска по газовому фактору может быть опасно
            ' так как возможна неоднозначность решения
            ' а текущий метод поиска работает только если есть одно решение
        Case 3
            Func = "calc_choke_dp_error_fw_atm"
            cal_type_string = "fw"
            val_min = 0
            val_max = 1
            If val_max > 1 Then val_max = 1
        Case 4
            Func = "calc_choke_dp_error_qliq_atm"
            cal_type_string = "qliq"
            val_min = 0
            val_max = choke.fluid.q_liq_sm3day * 1.5
        Case 5
            Func = "calc_choke_dp_error_qgas_atm"
            cal_type_string = "qgas_free"
            val_min = 0
            If q_gas_sm3day > 0 Then
                val_max = q_gas_sm3day * 2
            Else
                val_max = 10000
            End If
       Case Else
            ' solve_equation_bisection without initialasing func crashes excel
            MF_calibr_choke = "not implemented"
            Exit Function
    End Select
   
    prm.y_tolerance = const_pressure_tolerance
    If solve_equation_bisection(Func, val_min, val_max, CoeffA, prm) Then
    
        out = Array(prm.x_solution, _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    Else
        out = Array("no solution", _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    End If
        
    out_desc = Array("solution", _
                     "cal_type", _
                     "y_solution", _
                     "iterations", _
                     "description")
    MF_calibr_choke = array_join(Array(out, out_desc))
    
    Exit Function
   
    
    Exit Function
err1:
    MF_calibr_choke = -1
    addLogMsg "Error:MF_calibr_choke:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'  расчет распределения давления и температуры в трубопроводе
'  с использованием многофазных корреляций
Public Function MF_p_pipeline_atma( _
                 ByVal p_calc_from_atma As Double, _
                 ByVal t_calc_from_C As Double, _
                 ByVal t_val_C As Variant, _
                 ByVal h_list_m As Variant, _
                 ByVal diam_list_mm As Variant, _
                 ByVal q_liq_sm3day As Double, _
                 ByVal fw_perc As Double, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal calc_flow_direction As Integer = 11, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
        Optional ByVal c_calibr = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal out_curves As Integer = 1, _
        Optional ByVal out_curves_num_points As Integer = 20, _
        Optional ByVal num_value As Integer = 0, _
        Optional ByVal znlf As Boolean = False)
' p_calc_from_atma  - давление с которого начинается расчет, атм
'           граничное значение для проведения расчета
' t_calc_from_C - температура в точке где задано давление расчета
' t_val_C   - температура вдоль трубопровода
'           если число то температура на другом конце трубы
'           если range или таблица [0..N,0..1] то температура
'           окружающей среды по вертикальной глубине, С
' h_list_m  - траектория трубопровода, если число то измеренная
'           длина, range или таблица [0..N,0..1] то траектория
' diam_list_mm  - внутрнний диаметр трубы, если число то задается
'           постоянный диаметр, если range или таблица [0..N,0..1]
'           то задается зависимость диаметра от измеренной длины
' q_liq_sm3day - дебит жидкости в поверхностных условиях, нм3/сут
'           если q_liq_sm3day =0 и q_gas_sm3day > 0
'           тогда считается барботаж газа через жидкость
' fw_perc   - обводненность объемная в стандартных условиях
' q_gas_sm3day  - свободный газ нм3/сут. дополнительный к PVT потоку.
'           учитывается для барботажа или режима потока газа
'           в других случаях добавляется к общему потоку меняя rp
' str_PVT   - закодированная строка с параметрами PVT.
'           если задана - перекрывает другие значения
'           если задан флаг gas_only = 1 то жидкость не учитывается
' calc_flow_direction - направление расчета и потока относительно
'           координат.  11 расчет и поток по координате
'                       10 расчет по коордиате, поток против
'                       00 расчет и поток против координате
'                       01 расчет против координат, поток по
' hydr_corr - гидравлическая корреляция, H_CORRELATION
'           BeggsBrill = 0,
'           Ansari = 1,
'           Unified = 2,
'           Gray = 3,
'           HagedornBrown = 4,
'           SakharovMokhov = 5
' temp_method  - метод расчета температуры
'           0 - линейное распределение по длине
'           1 - температура равна температуре окружающей среды
'           2 - расчет температуры с учетом эмиссии в окр. среду
' c_calibr  - поправка на гравитационную составляющую
'           перепада давления, если дать ссылку на две ячейки,
'           то вторая будет поправка на трение.
' roughness_m - шероховатость трубы
' out_curves - флаг вывод значений между концами трубы
'           1 основные, 2 все значения.
'           вывод может замедлять расчет (не сильно)
' out_curves_num_points - количество точек для вывода значений
'           между концами трубы.
' num_value       - значение которое будет выводиться первым
' znlf    - флаг для расчета вертикального барботажа (дин уровень)
' результат - число - давление на другом конце трубы atma.
'           и распределение параметров по трубе
'description_end
    Dim pipe As New CPipe
    Dim res, res1
On Error GoTo err1:
    
    Set pipe = new_pipeline_with_stream(q_liq_sm3day, _
                                        fw_perc, _
                                        h_list_m, _
                                        t_calc_from_C, _
                                        calc_flow_direction, _
                                        str_PVT, _
                                        diam_list_mm, _
                                        hydr_corr, _
                                        t_val_C, _
                                        temp_method, _
                                        c_calibr, _
                                        roughness_m, _
                                        q_gas_sm3day, _
                                        znlf)
    
    ' calc pressure distribution
    res1 = PT_to_array(pipe.calc_dPipe(p_calc_from_atma, t_calc_from_C, allCurves))
    If out_curves = 2 Then
        res = pipe.array_out(out_curves_num_points, all_curves_out:=True)
    Else
        res = pipe.array_out(out_curves_num_points)
    End If
    res(0, 0) = res1(0)
    
    res(0, 0) = res(0, num_value)
    res(1, 0) = res(1, num_value)
    MF_p_pipeline_atma = res
    Exit Function
err1:
    MF_p_pipeline_atma = Array(-1, "error")
    addLogMsg "Error:MF_p_pipeline_atma:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет распределения давления и температуры в трубе
' (лучше не использовать - используйте MF_p_pipeline_atma)
Public Function MF_p_pipe_atma( _
        ByVal p_calc_from_atma As Double, _
        ByVal t_calc_from_C As Double, _
        ByVal t_calc_to_C As Double, _
        ByVal length_m As Double, _
        ByVal theta_deg As Double, _
        ByVal d_mm As Double, _
        ByVal q_liq_sm3day As Double, _
        ByVal fw_perc As Double, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal calc_flow_direction As Integer = 11, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal c_calibr = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal out_curves As Integer = 1, _
        Optional ByVal out_curves_num_points As Integer = 20, _
        Optional ByVal num_value As Integer = 0)
'p_calc_from_atma - давление с которого начинается расчет, атм
'                   граничное значение для проведения расчета
' t_calc_from_C   - температура в точке где задано давление, С
' t_calc_to_C     - температура на другом конце трубы
'                 по умолчанию температура вдоль трубы постоянна
'                   если задано то меняется линейно по трубе
' length_m        - Длина трубы, измеренная, м
' theta_deg       - угол направления потока к горизонтали
' d_mm            - внутренний диаметр трубы
' q_liq_sm3day     - дебит жидкости в поверхностных условиях
'              если q_liq_sm3day =0 и q_gas_sm3day > 0
'              тогда считается барботаж газа через жидкость
' fw_perc         - обводненность
' q_gas_sm3day    - свободный газ. дополнительный к PVT потоку.
' str_PVT         - закодированная строка с параметрами PVT.
'                   если задана - перекрывает другие значения
'        если задан флаг gas_only = 1 то жидкость не учитывается
' calc_flow_direction - направление расчета и потока
'                   относительно координат
'                   если = 11 расчет и поток по координате
'                   если = 10 расчет по, поток против координат
'                   если = 00 расчет и поток против координате
'                   если = 01 расчет против, поток по координате
' hydr_corr       - гидравлическая корреляция, H_CORRELATION
'                    BeggsBrill = 0
'                    Ansari = 1
'                    Unified = 2
'                    Gray = 3
'                    HagedornBrown = 4
'                    SakharovMokhov = 5
' c_calibr        - поправка на гравитационную составляющую
'           перепада давления, если дать ссылку на две ячейки,
'           то вторая будет поправка на трение
' roughness_m     - шероховатость трубы
' out_curves      - флаг вывод значений между концами трубы
'                   0 минимум, 1 основные, 2 все значения.
'                   вывод может замедлять расчет (не сильно)
' out_curves_num_points - количество точек для вывода значений
'                   между концами трубы.
' num_value       - значение которое будет выводиться первым
' результат       - число - давление на другом конце трубы atma.
'                  или массив - первая строка значения
'                               вторая строка - подписи
'description_end
    Dim pipe As CPipe
    Dim PVT As New CPVT
    Dim PTcalc As PTtype
    Dim PTin As PTtype
    Dim PTout As PTtype
    Dim TM As TEMP_CALC_METHOD
    Dim out, out_desc
    Dim out_curves_type As CALC_RESULTS
    Dim res
    Dim Arr
On Error GoTo err1:
    
    ' check pipe length
    length_m = Abs(length_m)                ' length must be positive
    If length_m = 0 Then
        MF_p_pipe_atma = Array(p_calc_from_atma, t_calc_from_C)
        Exit Function
    End If
    ' initialize pipe
    Set pipe = new_pipe_with_stream(q_liq_sm3day, fw_perc, length_m, calc_flow_direction, _
                                    str_PVT, theta_deg, d_mm, hydr_corr, _
                                    t_calc_from_C, t_calc_to_C, _
                                    c_calibr, _
                                    roughness_m, q_gas_sm3day)
    ' prep output
    If out_curves Then
        out_curves_type = allCurves
    Else
        out_curves_type = nocurves
    End If
    ' calc pressure distribution
    PTcalc = pipe.calc_dPipe(p_calc_from_atma, , out_curves_type)  ' ( -allcurves * out_curves ) can be used for simpcity but not
    
    ' prep results for output
    If calc_flow_direction \ 10 = 1 Then
        PTout = PTcalc
        PTin.p_atma = p_calc_from_atma
        PTin.t_C = t_calc_from_C
    Else
        PTin = PTcalc
        PTout.p_atma = p_calc_from_atma
        PTout.t_C = t_calc_from_C
    End If
    ' out results based on out_curves value
    If out_curves = 1 Then
        res = pipe.array_out(out_curves_num_points)
        res(0, 0) = PTcalc.p_atma
        res(0, 1) = PTcalc.t_C
        Arr = res
    ElseIf out_curves = 2 Then
        res = pipe.array_out(out_curves_num_points, all_curves_out:=True)
        res(0, 0) = PTcalc.p_atma
        res(0, 1) = PTcalc.t_C
        Arr = res
    Else
        out = Array(PTcalc.p_atma, _
                    PTcalc.t_C, _
                    PTin.p_atma, _
                    PTin.t_C, _
                    PTout.p_atma, _
                    PTout.t_C, _
                    pipe.c_calibr_grav, _
                    pipe.c_calibr_fric)
        
        out_desc = Array("p_calc_atma", _
                         "t_calc_C", _
                         "p_in_atma", _
                         "t_in_C", _
                         "p_out_atma", _
                         "t_out_C", _
                         "c_calibr_grav", _
                         "c_calibr_fric")
        
        Arr = array_join(Array(out, out_desc))
    
    End If
    Arr(0, 0) = Arr(0, num_value)
    Arr(1, 0) = Arr(1, num_value)
    MF_p_pipe_atma = Arr
    Exit Function
err1:
    MF_p_pipe_atma = Array(-1, "error")
    addLogMsg "Error:MF_p_pipe_atma:" & Err.Description
End Function
' ==============  функции для расчета штуцера ==========================
' =====================================================================
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет давления в штуцере, старый вариант
Public Function MF_p_choke_atma( _
            ByVal q_liq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal d_choke_mm As Double, _
            Optional ByVal p_calc_from_atma As Double = -1, _
            Optional ByVal calc_along_flow As Boolean = True, _
            Optional ByVal d_pipe_mm As Double = 70, _
            Optional ByVal t_choke_C As Double = 20, _
            Optional ByVal c_calibr_fr As Double = 1, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal q_gas_sm3day As Double = 0)
' q_liq_sm3day   - дебит жидкости в поверхностных условиях
' fw_perc       - обводненность
' d_choke_mm     - диаметр штуцера (эффективный)
' p_calc_from_atma    - давление с которого начинается расчет, атм
'                 граничное значение для проведения расчета
'                 либо давление на входе, либо на выходе
' calc_along_flow - флаг направления расчета относительно потока
'     если = 1 то расчет по потоку
'     ищется давление на выкиде по известному давлению на входе,
'     ищется линейное давление по известному буферному
'     если = 0 то расчет против потока
'     ищется давление на входе по известному давлению на выходе,
'     ищется буферное давление по известному линейному
' d_pipe_mm      - диаметр трубы до и после штуцера
' t_choke_C      - температура, С.
' c_calibr_fr   - поправочный коэффициент на штуцер
'                 1 - отсутсвие поправки
'                 Q_choke_real = c_calibr_fr * Q_choke_model
' str_PVT        - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' результат     - число - давления на штуцере на расчетной стороне.
'                массив значений с параметрами штуцера
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim PT As PTtype
    Dim pres As Double
    Dim PVT As CPVT
    Dim out, out_desc
    Dim p_in_atma As Double
    Dim p_out_atma As Double
    
    
    Call PVT.init_feed(str_PVT)
    PVT.q_gas_free_sm3day = q_gas_sm3day
    Set choke.fluid = PVT
    choke.fluid.q_liq_sm3day = q_liq_sm3day
    choke.fluid.fw_perc = fw_perc
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    choke.c_calibr_fr = c_calibr_fr
    If PVT.gas_only Then
        pres = GLV_p_atma(d_choke_mm, p_calc_from_atma, q_gas_sm3day, PVT.gamma_g, t_choke_C, calc_along_flow)(0)(0)
        out = pres
        If calc_along_flow Then
            p_in_atma = p_calc_from_atma
            p_out_atma = out
            out_desc = "Pout, atma"
        Else
            p_out_atma = p_calc_from_atma
            p_in_atma = out
            out_desc = "Pin, atma"
        End If
    Else
        If calc_along_flow Then
            p_in_atma = p_calc_from_atma
            PT = choke.calc_choke_p_lin(set_PT(p_in_atma, t_choke_C))
            out = PT.p_atma
            p_out_atma = out
            out_desc = "Pout, atma"
        Else
            p_out_atma = p_calc_from_atma
            PT = choke.calc_choke_p_buf(set_PT(p_out_atma, t_choke_C))
            out = PT.p_atma
            p_in_atma = out
            out_desc = "Pin, atma"
        End If
    End If
    MF_p_choke_atma = Array(Array(out, p_in_atma, p_out_atma, t_choke_C, choke.c_calibr_fr, PVT.q_gas_sm3day), _
                            Array(out_desc, "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr", "q_gas_sm3day"))  ' на выходе выдаем массив
    MF_p_choke_atma = array_join(MF_p_choke_atma)
    Exit Function
err1:
    MF_p_choke_atma = -1
    addLogMsg "Error:MF_p_choke_atma:" & Err.Description
End Function
Public Function MF_choke_q_sm3day( _
        ByVal d_choke_mm As Double, _
        ByVal p_in_atma As Double, _
        ByVal p_out_atma As Double, _
        ByVal fw_perc As Double, _
        Optional ByVal t_choke_C = 20, _
        Optional ByVal d_pipe_mm As Double = 70, _
        Optional ByVal c_calibr_fr As Double = 1, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal q_gas_sm3day As Double = 0)
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет давления в штуцере
Public Function MF_choke_p_atma( _
                     ByVal d_choke_mm As Double, _
                     ByVal p_calc_from_atma As Double, _
                     ByVal q_liq_sm3day As Double, _
                     ByVal fw_perc As Double, _
            Optional ByVal t_choke_C As Double = 20, _
            Optional ByVal d_pipe_mm As Double = 70, _
            Optional ByVal param As String = "", _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal q_gas_sm3day As Double = 0)
' d_choke_mm    - диаметр штуцера (эффективный)
' p_calc_from_atma - давление с которого начинается расчет, атм
'                    граничное значение для проведения расчета
'                    либо давление на входе, либо на выходе
' q_liq_sm3day   - дебит жидкости в поверхностных условиях
' fw_perc       - обводненность
' t_choke_C      - температура потока, С.
' d_pipe_mm      - диаметр трубы до и после штуцера
' param          - параметры расчета json строка
' str_PVT        - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' результат     - число - давления на штуцере на расчетной стороне.
'                массив значений с параметрами штуцера
'description_end
Const nv = 9  ' output array dimension (number of columns)
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim res(0 To 2, 0 To nv) As Variant
        
    ' param dictionary and settings variables with default values
    Dim prmd As Dictionary
    Dim calc_along_flow As Boolean
    calc_along_flow = False
    Dim show_log As Boolean
    show_log = 1
    Dim num_value As Integer
    num_value = 1
    Dim calibr As Double
    calibr = 1
    ' auxiliary variables
    Dim log_str As String
    Dim i As Integer
    Dim PT As PTtype
    Dim pres As Double
    Dim out, out_desc
    Dim p_in_atma As Double
    Dim p_out_atma As Double
    With choke
    On Error Resume Next
        ' decode param here
        Set prmd = ParseJson(param)
        If prmd Is Nothing Then
            .addLogMsg "info: no param found, default settings applied"
        End If
        ' for references to parameter names we use constants to make it easier to manage later.
        ' by default the variables will be set to zero or empty (if no key is found)
        calc_along_flow = prmd(prm_calc_along_flow)
        Call .addLogMsg("info: calc_along_flow = " & calc_along_flow)
    
        show_log = prmd(prm_show_log)
        
        num_value = prmd(prm_num_value)
        If num_value < 1 Or num_value > nv Then num_value = 1
        
        calibr = prmd(prm_choke_calibr)
        If calibr < 0.00001 Then calibr = 1
        Call .addLogMsg("info: choke_calibr = " & calibr)
    
    On Error GoTo err1:
        
        Call .fluid.init_feed(str_PVT)
        .fluid.q_gas_free_sm3day = q_gas_sm3day
        .fluid.q_liq_sm3day = q_liq_sm3day
        .fluid.fw_perc = fw_perc
        .d_down_m = d_pipe_mm / 1000
        .d_up_m = d_pipe_mm / 1000
        .d_choke_m = d_choke_mm / 1000
        .c_calibr_fr = calibr
        
        If .fluid.gas_only Then
            pres = GLV_p_atma(d_choke_mm, p_calc_from_atma, q_gas_sm3day, .fluid.gamma_g, t_choke_C, calc_along_flow)(0)(0)
            out = pres
            If calc_along_flow Then
                p_in_atma = p_calc_from_atma
                p_out_atma = out
                out_desc = "Pout, atma"
            Else
                p_out_atma = p_calc_from_atma
                p_in_atma = out
                out_desc = "Pin, atma"
            End If
        Else
            If calc_along_flow Then
                p_in_atma = p_calc_from_atma
                PT = .calc_choke_p_lin(set_PT(p_in_atma, t_choke_C))
                out = PT.p_atma
                p_out_atma = out
                out_desc = "Pout, atma"
            Else
                p_out_atma = p_calc_from_atma
                PT = .calc_choke_p_buf(set_PT(p_out_atma, t_choke_C))
                out = PT.p_atma
                p_in_atma = out
                out_desc = "Pin, atma"
            End If
        End If
        
err1done:
    
    On Error Resume Next
        res(0, 1) = p_in_atma:                  res(1, 1) = "p_intake_atma"
        res(0, 2) = p_out_atma:                 res(1, 2) = "p_out_atma"
        res(0, 3) = t_choke_C:                  res(1, 3) = "t_choke_C"
        res(0, 4) = .c_calibr_fr:               res(1, 4) = "c_calibr_fr"
        res(0, 5) = .fluid.q_liq_sm3day:        res(1, 5) = "q_liq_sm3day"
        res(0, 6) = .fluid.q_gas_sm3day:        res(1, 6) = "q_gas_sm3day"
        res(0, 7) = .t_choke_throat_C:          res(1, 7) = "t_choke_throat_C"
        res(0, 8) = .sonic_vel_msec:            res(1, 8) = "sonic_vel_msec"
        res(0, 9) = .q_liq_max_sm3day:          res(1, 9) = "q_liq_max_sm3day"
        
        res(0, 0) = res(0, num_value): res(1, 0) = res(1, num_value)
        
        If show_log Then
            log_str = .log.get_str
            If Len(log_str) > 32000 Then
                log_str = "log too long"
            End If
        Else
            log_str = "[""set show_log=1 in param to show calc log""]"
        End If
        res(2, 0) = log_str
        For i = 1 To nv
            res(2, i) = ""
        Next i
    End With
    MF_choke_p_atma = res
    Exit Function
err1:
    MF_choke_p_atma = -1
    choke.addLogMsg "error:MF_p_choke_atma:" & Err.Description
    Resume err1done:
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет корректирующего фактора (множителя) модели штуцера под замеры
' быстрый расчет - калибровка вычисляется
Public Function MF_calibr_choke_fast( _
            ByVal q_liq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal d_choke_mm As Double, _
            Optional ByVal p_in_atma As Double = -1, _
            Optional ByVal p_out_atma As Double = -1, _
            Optional ByVal d_pipe_mm As Double = 70, _
            Optional ByVal t_choke_C As Double = 20, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal q_gas_sm3day As Double = 0)
' q_liq_sm3day   - дебит жидкости в ст. условиях
' fw_perc       - обводненность
' d_choke_mm    - диаметр штуцера (эффективный), мм
' p_in_atma     - давление на входе (высокой стороне)
' p_out_atma    - давление на выходе (низкой стороне)
' d_pipe_mm     - диаметр трубы до и после штуцера, мм
' t_choke_C     - температура, С.
' str_PVT       - закодированная строка с параметрами PVT,
'                 если задана - перекрывает другие значения
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' результат     - число - калибровочный коэффициент для модели.
'                 штуцера  - множитель на дебит через штуцер
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim PT As PTtype
    Dim PVT As CPVT
    Dim out, out_desc
    
    Call PVT.init_feed(str_PVT)
    
    If PVT.gas_only Then
        MF_calibr_choke_fast = "not implemented yet"
        Exit Function
    End If
    
    PVT.q_gas_free_sm3day = q_gas_sm3day
    Set choke.fluid = PVT
    choke.fluid.q_liq_sm3day = q_liq_sm3day
    choke.fluid.fw_perc = fw_perc
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    
    If p_in_atma > p_out_atma And p_out_atma >= 1 Then
        Call choke.calc_choke_calibration(p_in_atma, p_out_atma, t_choke_C)
        out = choke.c_calibr_fr
        out_desc = "c_calibr_fr"
    End If
    MF_calibr_choke_fast = Array(Array(out, p_in_atma, p_out_atma, t_choke_C, choke.c_calibr_fr), _
                                 Array(out_desc, "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr"))  ' на выходе выдаем массив
    MF_calibr_choke_fast = array_join(MF_calibr_choke_fast)
    Exit Function
err1:
    MF_calibr_choke_fast = -1
    addLogMsg "Error:MF_calibr_choke_fast:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
 ' функция расчета дебита жидкости через штуцер
 ' при заданном входном и выходном давлениях
Public Function MF_q_choke_sm3day( _
        ByVal fw_perc As Double, _
        ByVal d_choke_mm As Double, _
        ByVal p_in_atma As Double, _
        ByVal p_out_atma As Double, _
        Optional ByVal d_pipe_mm As Double = 70, _
        Optional ByVal t_choke_C = 20, _
        Optional ByVal c_calibr_fr As Double = 1, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal q_gas_sm3day As Double = 0)
' fw_perc      - обводненность
' d_choke_mm   - диаметр штуцера (эффективный)
' p_in_atma    - давление на входе (высокой стороне)
' p_out_atma   - давление на выходе (низкой стороне)
' d_pipe_mm    - диаметр трубы до и после штуцера
' t_choke_C    - температура, С.
' c_calibr_fr  - поправочный коэффициент на штуцер
'                 1 - отсутсвие поправки (по умолчанию)
'                 Q_choke_real = c_calibr_fr * Q_choke_model
' str_PVT      - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' q_gas_sm3day - дополнительный поток свободного газа
' результат    - расход и массив результатов
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim PVT As CPVT
    Dim q As Double
    
    Call PVT.init_feed(str_PVT)
    Set choke.fluid = PVT
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    choke.fluid.fw_perc = fw_perc
    choke.fluid.q_gas_free_sm3day = q_gas_sm3day
    choke.c_calibr_fr = c_calibr_fr
    
    If PVT.gas_only Then
        q = GLV_q_gas_sm3day(d_choke_mm, p_in_atma, p_out_atma, PVT.gamma_g, t_choke_C, c_calibr_fr)(0)(0)
        MF_q_choke_sm3day = Array(Array(q, p_in_atma, p_out_atma, t_choke_C, c_calibr_fr), _
                            Array("Qgas", "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr"))
    Else
        q = choke.calc_choke_q_liq_sm3day(p_in_atma, p_out_atma, t_choke_C)
        MF_q_choke_sm3day = Array(Array(q, p_in_atma, p_out_atma, t_choke_C, c_calibr_fr), _
                            Array("Qliq", "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr"))
    End If
    MF_q_choke_sm3day = array_join(MF_q_choke_sm3day)
    Exit Function
err1:
    MF_q_choke_sm3day = -1
    addLogMsg "Error:MF_q_choke_sm3day:" & Err.Description
End Function

